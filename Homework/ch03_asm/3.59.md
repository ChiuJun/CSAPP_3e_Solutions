```c
typedef __int128 int128_t

//              dest in %rdi    x in %rsi   y in %rdx
void store_prod(int128_t *dest, int64_t x, int64_t y){
    *dest = x * (int128_t)y;
}
```
- 准备工作
```x86asm
;64位有符号数符号扩展至128位有符号数

;将 %rdx 也就是 y 存储至 %rax ，为 y 符号扩展至8字做准备
movq %rdx,%rax

;cqto 将 %rax 也就是 y 由4字(quad)符号扩展至(to)8字(oct)
;y 存储在 %rdx:%rax
;即 yH==%rdx yL==%rax y==yH*2^64 + yL
cqto

;将 %rsi 也就是 x 存储至 %rcx ，为 x 符号扩展至8字做准备
movq %rsi,%rcx

;将 %rcx 算术右移63位，由此 %rcx 被 x 的符号位填充
;x 存储在 %rcx:%rsi
;即 xH==%rcx xL==%rsi x==xH*2^64 +xL
sarq $63,%rcx
```
- 计算
```x86asm
;设乘积为128位有符号数p，高64位为pH，低64位为pL  
;容易得到 p  = x*y  
;           = (xH*2^64 + xL)*(yH*2^64 + yL)  
;           = xH*yH*2^128 + xH*yL*2^64 + yH*xL*2^64 + xL*yL  
;截断为128位即   
;        p  = xH*yL*2^64 + yH*xL*2^64 + xL*yL

;计算 xH*yL 并存储在 %rcx
imulq %rax,%rcx 

;计算 yH*xL 并存储在 %rdx
imulq %rsi,%rdx

;计算 xH*yL + yH*xL 并存储在 %rcx
addq %rdx,%rcx

;计算 xL*yL 并存储在 %rdx:%rax
;即 p 暂存在 %rdx:%rax
mulq %rsi

;计算 pH
addq %rcx,%rdx
```
- 存储并返回
```x86asm
;将 pL 按小端机器的规则存放在 %rdi 指向的低8字节
movq %rax,(%rdi)

;将 pL 按小端机器的规则存放在 %rdi 指向的高8字节
movq %rdx,8(%rdi)

;返回主调程序
ret
```